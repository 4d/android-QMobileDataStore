apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: "$rootDir/buildscripts/ktlint.gradle"
apply from: "$rootDir/buildscripts/testlog.gradle"
apply from: "$rootDir/buildscripts/detekt.gradle"
apply from: "$rootDir/buildscripts/jacoco.gradle"
apply from: "$rootDir/buildscripts/projectDependencyGraph.gradle"

dependencies {

    // Common
    implementation Libs.androidx_appcompat
    implementation Libs.kotlin_stdlib

    // Room
    implementation Libs.androidx_room
    implementation Libs.androidx_room_runtime
    kapt Libs.androidx_room_compiler

    // Utils
    implementation Libs.retrofit_converter_gson

    // Testing
    testImplementation Libs.junit
}

def libraryGroupId = 'com.qmobile.qmobiledatastore'
def libraryArtifactId = 'qmobiledatastore'
def libraryVersion = '0.0.1'
def buildVariant = 'release'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion

            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/${artifactId}-${buildVariant}.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("${buildVariant}CompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

artifactory {
    def machineIP = System.getenv('ARTIFACTORY_MACHINE_IP')
    contextUrl = "http://${machineIP}:8081/artifactory"
    publish {
        repository {
            repoKey = "libs-release-local"
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
        }
    }
}

task generateMySources(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
task addMySourcesToAar(type: Jar) {
    archiveFileName.set("${libraryArtifactId}WithSources.aar")
    destinationDirectory.set(file("build"))
    from zipTree("build/outputs/aar/${libraryArtifactId}-${buildVariant}.aar")
    from fileTree("build").include("libs/${libraryArtifactId}-sources.jar")
}
afterEvaluate { project ->
    project.tasks.preBuild.dependsOn generateMySources
    project.addMySourcesToAar.dependsOn build
}
artifacts {
    archives addMySourcesToAar.archivePath
}